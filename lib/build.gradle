apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    ndkVersion '21.4.7075529'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dokkaHtml {
    moduleName.set("${rootProject.name}")

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            skipDeprecated.set(true)
            includeNonPublic.set(false)
            skipEmptyPackages.set(true)
        }
    }
}

ext {
    isReleasedVersion = !project.version.endsWith("-SNAPSHOT")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "io.github.thibaultbee"
                artifactId = "srtdroid"
                version = "${rootProject.versionName}"

                pom {
                    name = "srtdroid"
                    packaging = "aar"
                    description = "Secure Reliable Transport (SRT) Protocol for Android"
                    url = "https://github.com/ThibaultBee/srtdroid"

                    scm {
                        url = "https://github.com/ThibaultBee/srtdroid"
                        connection = "scm:git:git://github.com/ThibaultBee/srtdroid.git"
                        developerConnection = "scm:git:ssh://github.com/ThibaultBee/srtdroid.git"
                    }

                    licenses {
                        license {
                            name = "Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            name = "Thibault B. & SRT team"
                            organizationUrl = "https://github.com/ThibaultBee"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                if (!isReleasedVersion) {
                    url "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                }
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
        signing {
            required { isReleasedVersion && gradle.taskGraph.hasTask("uploadArchives") }
            sign publishing.publications.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.google.guava:guava:30.0-jre'

}
